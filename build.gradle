import org.jetbrains.dokka.gradle.DokkaMultiModuleTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.9.23'
    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'com.android.application' version '8.4.0' apply false
    id 'com.android.library' version '8.4.0' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.devtools.ksp' version '1.9.23-1.0.20' apply false
    id 'com.diffplug.spotless' version '6.25.0'
    id "org.jetbrains.dokka" version "1.9.20"
}

subprojects {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude "${layout.buildDirectory}/**/*.kt"
            targetExclude 'bin/**/*.kt'

            ktlint()
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }

        format('kts') {
            target '**/*.kts'
            targetExclude '**/build/**/*.kts'
            // Look for the first line that doesn't have a block comment (assumed to be the license)
            licenseHeaderFile rootProject.file('spotless/copyright.kts'), '(^(?![\\/ ]\\*).*$)'
        }

        format('xml') {
            target '**/*.xml'
            targetExclude '**/build/**/*.xml'
            // Look for the first XML tag that isn't a comment (<!--) or the xml declaration (<?xml)
            licenseHeaderFile rootProject.file('spotless/copyright.xml'), '(<[^!?])'
        }
    }
}

tasks.withType(DokkaMultiModuleTask).configureEach {
    outputDirectory = rootProject.file('docs/api')
    failOnWarning = true
}
