import org.jetbrains.dokka.gradle.DokkaMultiModuleTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '2.1.21'
    ext.dokka_version = '2.0.0'

    dependencies {
        classpath("org.jetbrains.dokka:dokka-base:$dokka_version")
    }

    repositories {
        google()
        mavenCentral()
        gradlePluginPortal()
    }
}

plugins {
    id 'com.android.application' version '8.10.1' apply false
    id 'com.android.library' version '8.10.1' apply false
    id 'org.jetbrains.kotlin.android' version "$kotlin_version" apply false
    id 'com.google.devtools.ksp' version '2.1.21-2.0.2' apply false
    id 'com.diffplug.spotless' version '7.0.4'
    id "org.jetbrains.dokka" version "$dokka_version"
}

subprojects {
    apply plugin: 'com.diffplug.spotless'

    spotless {
        kotlin {
            target '**/*.kt'
            targetExclude "${layout.buildDirectory}/**/*.kt"
            targetExclude 'bin/**/*.kt'

            ktlint()
            licenseHeaderFile rootProject.file('spotless/copyright.kt')
        }

        format('kts') {
            target '**/*.kts'
            targetExclude '**/build/**/*.kts'
            // Look for the first line that doesn't have a block comment (assumed to be the license)
            licenseHeaderFile rootProject.file('spotless/copyright.kts'), '(^(?![\\/ ]\\*).*$)'
        }

        format('xml') {
            target '**/*.xml'
            targetExclude '**/build/**/*.xml'
            // Look for the first XML tag that isn't a comment (<!--) or the xml declaration (<?xml)
            licenseHeaderFile rootProject.file('spotless/copyright.xml'), '(<[^!?])'
        }
    }
}

tasks.withType(DokkaMultiModuleTask).configureEach {
    moduleName = "Why Not! Image Carousel!"
    outputDirectory = rootProject.file('docs/api')
    failOnWarning = true

    pluginsMapConfiguration.set(
            [
                    "org.jetbrains.dokka.base.DokkaBase": """{
                      "footerMessage": "Copyright Â© 2021 Md. Mahmudul Hasan Shohag"
                    }"""
            ]
    )
}
