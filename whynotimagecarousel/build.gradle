import com.vanniktech.maven.publish.SonatypeHost

plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id "com.vanniktech.maven.publish" version "0.32.0"
    id "org.jetbrains.dokka" version "$dokka_version"
}

android {
    namespace 'org.imaginativeworld.whynotimagecarousel'
    compileSdk 36

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 36
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.core:core-ktx:1.16.0'

    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.4.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.16.0'

    // Circle Indicator
    implementation 'me.relex:circleindicator:2.1.6'
}

mavenPublishing {
    // Configure Maven Central

    publishToMavenCentral(SonatypeHost.CENTRAL_PORTAL, true)
    signAllPublications()

    // Configure the POM

    coordinates("org.imaginativeworld.whynotimagecarousel", "whynotimagecarousel", "2.1.1")

    pom {
        name = "Why Not! Image Carousel!"
        description = "An easy, super simple and customizable image carousel view for Android."
        inceptionYear = "2020"
        url = "https://github.com/ImaginativeShohag/Why-Not-Image-Carousel"

        licenses {
            license {
                name = "The Apache Software License, Version 2.0"
                url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                distribution = "repo"
            }
        }

        developers {
            developer {
                id = "ImaginativeShohag"
                name = "Md. Mahmudul Hasan Shohag"
                url = "https://github.com/ImaginativeShohag"
            }
        }

        scm {
            url = "https://github.com/ImaginativeShohag/Why-Not-Image-Carousel"
            connection = "scm:git:git://github.com/ImaginativeShohag/Why-Not-Image-Carousel.git"
            developerConnection = "scm:git:ssh://git@github.com/ImaginativeShohag/Why-Not-Image-Carousel.git"
        }
    }
}

dokkaHtmlPartial {
    pluginsMapConfiguration.set(
            [
                    "org.jetbrains.dokka.base.DokkaBase": """{
                      "footerMessage": "Copyright Â© 2020 Md. Mahmudul Hasan Shohag"
                    }"""
            ]
    )
}